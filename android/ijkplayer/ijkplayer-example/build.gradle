if (isModuleAsApp.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    // http://tools.android.com/tech-docs/new-build-system/tips
    //noinspection GroovyAssignabilityCheck
    compileSdkVersion rootProject.ext.compileSdkVersion
    //noinspection GroovyAssignabilityCheck
    buildToolsVersion rootProject.ext.buildToolsVersion

    lintOptions {
        abortOnError false
    }
    defaultConfig {
        if (isModuleAsApp.toBoolean()) {
            applicationId "tv.danmaku.ijk.media.example"
            versionCode rootProject.ext.versionCode
            versionName rootProject.ext.versionName
        }
        minSdkVersion 9
        targetSdkVersion rootProject.ext.targetSdkVersion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    if (isModuleAsApp.toBoolean()) {
        productFlavors {
            all32 { minSdkVersion 9 }
            all64 { minSdkVersion 21 }
            // armv5 {}
            // armv7a {}
            // arm64 { minSdkVersion 21 }
            // x86 {}
        }
    } else {
        // 老版本的gradle中library不支持配置flavor...
    }
    sourceSets {
        main {
            if (!isModuleAsApp.toBoolean()) {
                /* listFiles(): 列出path中除excludes以外的所有文件*/
                def listFiles = { String path, String[] excludes, String outPath = 'src/main/java/' ->
                    def canonicalPath = file(path).canonicalPath
                    def canonicalOutPath = file(outPath).canonicalPath
                    def prefix = canonicalPath.substring(canonicalOutPath.length() + 1).replace('\\', '/')
                    file(path).list().findAll { !excludes.contains(it) }.collect {
                        "$prefix/$it"
                    }
                }

                // 重定向 清单文件
                manifest.srcFile 'src/module/AndroidManifest.xml'
                // 重定向 res文件
                res.srcDirs = ['src/module/res']
                // 排除不需要的Java文件
                java {
                    exclude '**/*Activity.java'
                    exclude listFiles('src/main/java/tv/danmaku/ijk/media/example/fragments', /*excludes: */ 'SettingsFragment.java')
                    exclude 'tv/danmaku/ijk/media/example/content/**'
                    exclude 'tv/danmaku/ijk/media/example/eventbus/**'

                }
            }
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile "junit:junit:4.12"
    androidTestCompile("com.android.support.test:runner:1.0.2") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.android.support:preference-v7:23.0.1'
    compile 'com.android.support:support-annotations:23.0.1'

    compile 'com.squareup:otto:1.3.8'

    compile project(':ijkplayer-java')
    compile project(':ijkplayer-exo')

    if (isModuleAsApp.toBoolean()) {
        all32Compile project(':ijkplayer-armv5')
        all32Compile project(':ijkplayer-armv7a')
        all32Compile project(':ijkplayer-x86')

        all64Compile project(':ijkplayer-armv5')
        all64Compile project(':ijkplayer-armv7a')
        all64Compile project(':ijkplayer-arm64')
        all64Compile project(':ijkplayer-x86')
        all64Compile project(':ijkplayer-x86_64')
    } else {
        compile project(':ijkplayer-armv7a')
        compile project(':ijkplayer-x86')
    }

    // compile 'tv.danmaku.ijk.media:ijkplayer-java:0.8.4'
    // compile 'tv.danmaku.ijk.media:ijkplayer-exo:0.8.4'

    // all32Compile 'tv.danmaku.ijk.media:ijkplayer-armv5:0.8.4'
    // all32Compile 'tv.danmaku.ijk.media:ijkplayer-armv7a:0.8.4'
    // all32Compile 'tv.danmaku.ijk.media:ijkplayer-x86:0.8.4'

    // all64Compile 'tv.danmaku.ijk.media:ijkplayer-armv5:0.8.4'
    // all64Compile 'tv.danmaku.ijk.media:ijkplayer-armv7a:0.8.4'
    // all64Compile 'tv.danmaku.ijk.media:ijkplayer-arm64:0.8.4'
    // all64Compile 'tv.danmaku.ijk.media:ijkplayer-x86:0.8.4'
    // all64Compile 'tv.danmaku.ijk.media:ijkplayer-x86_64:0.8.4'

    // armv5Compile project(':player-armv5')
    // armv7aCompile project(':player-armv7a')
    // arm64Compile project(':player-arm64')
    // x86Compile project(':player-x86')
    // x86_64Compile project(':player-x86_64')
}

if (!isModuleAsApp.toBoolean()) {
    apply from: new File(rootProject.projectDir, "tools/gradle-on-demand.gradle");
}